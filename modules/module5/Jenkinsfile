pipeline {
  agent none

  environment {
    docker_label="nljenkinsagent"
    nlw_host="nlweb.shared"
    api_url="http://${env.nlw_host}:8080"
    zone_id="${ZONE_ID}"
  }

  stages {
    stage ('Prep workspace') {
      agent any
      steps {
        cleanWs()
        script {
          sh "uname -a"
          env.host_ip = sh(script: "getent hosts ${env.nlw_host} | head -n1 | grep -oE '((1?[0-9][0-9]?|2[0-4][0-9]|25[0-5])\\.){3}(1?[0-9][0-9]?|2[0-4][0-9]|25[0-5])'", returnStdout: true)
        }
      }
    }
    stage ('Check/Build Docker Agent') {
      agent any
      steps {
        script {
          imgCount = sh(script: "docker images -a --filter='label=${env.docker_label}' --format='{{.ID}}' | wc -l", returnStdout: true).toInteger()
          if(imgCount < 1)
            docker.build("${env.docker_label}:latest", "--rm --label '${env.docker_label}' -f ./infra/JenkinsBuildAgent.Dockerfile .")
        }
      }
    }
    stage('Attach Worker') {
      agent {
        docker {
          image "${env.docker_label}:latest"
          args "--add-host ${env.nlw_host}:${env.host_ip} -e HOME=${env.WORKSPACE} -e PYTHONUNBUFFERED=1"
        }
      }
      stages {
        stage('Prepare agent') {
          steps {
            sh 'neoload --version'
            withCredentials([string(credentialsId: 'NLW_TOKEN', variable: 'NLW_TOKEN')]) {
              sh "neoload login --url ${env.api_url} $NLW_TOKEN"
            }
          }
        }
        stage('Prepare Neoload test') {
          steps {
            script {
              def zone_id = env.zone_id
              if(zone_id.trim().toLowerCase().equals("null")) zone_id = ""

              if(zone_id.trim().length() < 1) // dynamically pick a zone
                zone_id = sh(script: "neoload zones | jq '[.[]|select((.controllers|length>0) and (.loadgenerators|length>0) and (.type==\"STATIC\"))][0] | .id' -r", returnStdout: true).trim()

              if(zone_id == null)
                error "No zones with available infrastructure were found! Please run 'Start Infra' job."

              sh "neoload test-settings --zone ${zone_id} --lgs 1 --scenario sanityScenario createorpatch 'example-Jenkins-module5'"
            }
          }
        }
        stage('Prepare Test Assets') {
          steps {
            writeFile(file: "d.sanity.yaml", text:"""
scenarios:
- name: sanityScenario
  populations:
  - name: popGets
    constant_load:
      users: 2
      duration: 30s
            """)
            stash includes: 'd.*.yaml', name: 'dynamics'
          }
        }
        stage('Upload Test Assets') {
          steps {
            dir('tests/neoload_projects/example_1/') {
              unstash 'dynamics'
            }
            sh "neoload project --path tests/neoload_projects/example_1/ upload"
            sh "neoload status"
          }
        }
        stage('Run a sanity scenario') {
          steps {
            dir('tests/neoload_projects/example_1/') {
              unstash 'dynamics'
            }
            script {
              sanityCode = 3 // default to something absurd
              try {
                sanityCode = sh(script: "neoload run --scenario sanityScenario --as-code default.yaml,d.sanity.yaml", returnStatus: true)
              } catch(error) {
                error "Sanity test kickoff error ${error}"
              } finally {
                print "Sanity status code was ${sanityCode}"
                if(sanityCode > 1)
                  error "Sanity test failed so not proceeding to full test!"
                else
                  sh "neoload test-results delete cur" // get rid of successful sanity run results
              }
            }
          }
        }
        stage('Run Test') {
          stages {
            stage('Kick off test async') {
              steps {
                sh """neoload run \
                  --scenario fullTest \
                  --detached \
                  --as-code default.yaml
                 """
              }
            }
            stage('Monitor test') {
              parallel {
                stage('Monitor SLAs') {
                  steps {
                    script {
                      logs_url = sh(script: "neoload logs-url cur", returnStdout: true).trim()
                      echo "Logs url: ${logs_url}"

                      sh "neoload fastfail --max-failure 25 slas cur"
                    }
                  }
                }
                stage('Custom test exit criteria') {
                  steps {
                    script {
                      sleep(time:15,unit:"SECONDS")
                    }
                  }
                }
                stage('Wait for test finale') {
                  steps {
                    script {
                      env.exitCode = sh(script: "neoload wait cur", returnStatus: true)
                      print "Final status code was ${env.exitCode}"
                    }
                  }
                }
              } //end parallel
            }
          } // end stages
          post {
            always {
              sh "neoload test-results junitsla"
              junit testResults: 'junit-sla.xml', allowEmptyResults: true
            }
          }
        }
      }
    }
  }
}
